plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'maven-publish'

group 'com.atatctech'
version '1.0.1b8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.1'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'com.atatctech:packages:1.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption('charset', 'UTF-8')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hephaestus'
            description = 'The most suitable text language for documentation. Native support for Markdown and HTML.'
            from components.java

            pom {
                name = 'Hephaestus'
                description = 'The most suitable text language for documentation. Native support for Markdown and HTML.'
                url = 'https://github.com/ATATCTech/Hephaestus'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ATATCTech'
                        name = 'ATATCTech'
                        email = 'email@atatctech.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/ATATCTech/Hephaestus.git'
                    developerConnection = 'scm:git:https://github.com/ATATCTech/Hephaestus.git'
                    url = 'https://github.com/ATATCTech/Hephaestus'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}

test {
    useJUnitPlatform()
}
